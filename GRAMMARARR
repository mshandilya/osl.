<Prog> ::= <Decl> | <Decl> <Prog>
<Decl> ::= <VarDecl> | <Stmt> | <FunDecl>
<FunCall> ::= IDEN <Calls>
<Calls> ::= <Call> | <Call> <Calls>
<Call> ::= LPAREN <CallParams> RPAREN | LPAREN RPAREN
<CallParams> ::= <Val> | <Val> COMMA <CallParams>
<FunDecl> ::= FUN <GenType> IDEN LPAREN <Params> RPAREN <Block> | FUN <GenType> IDEN LPAREN RPAREN <Block>
<Params> ::= <Param> | <Param> COMMA <Params>
<Param> ::= <GenType> IDEN
<Stmt> ::= <ExpStmt> | <Block> | <IfStmt> | <WhileStmt> | <LogStmt> | <RetStmt>
<Block> ::= LBRACE RBRACE | LBRACE <Prog> RBRACE
<RetStmt> ::= RETURN <ExpStmt>
<IfStmt> ::= IF <Val> <Block> | IF <Val> <Block> <ElseStmt> | IF <Val> <Block> <ElifStmts> | IF <Val> <Block> <ElifStmts> <ElseStmt>
<ElseStmt> ::= ELSE <Block>
<ElifStmts> ::= <ElifStmt> | <ElifStmt> <ElifStmts>
<ElifStmt> ::= ELIF <Val> <Block>
<WhileStmt> ::= WHILE <Val> <Block>
<LogStmt> ::= LOG <ExpStmt>
<ExpStmt> ::= <Val> SEMC
<VarDecl> ::= <Mutable> | <Immutable>
<Mutable> ::= VAR <DeclType> IDEN SEMC | VAR <DeclType> <Assn> SEMC
<Immutable> ::= CONST <DeclType> <Assn> SEMC
<Val> ::= <Exp> | <Assn> | NULL
<Assn> ::= <Loc> ASSIGN <Val>
<Exp> ::= <UnAmb> | <Or> | <And> | <Not> | <Lesser> | <LessEqual> | <Greater> | <GreatEqual> | <Eq> | <NotEq> | <Add> | <Multiply> | <Divide> | <Subtract> | <Power> | <Modulo> | <Xor> | <Xand> | <LeftShift> | <RightShift> | <UnaryNeg>
<Or> ::= <UnAmb> OR <UnAmb> | <UnAmb> OR <Or>
<And> ::= <UnAmb> AND <UnAmb> | <UnAmb> AND <And>
<Not> ::= NOT <UnAmb> | NOT <Not>
<Lesser> ::= <UnAmb> LE <UnAmb> | <UnAmb> LE <Lesser> | <UnAmb> LE <LessEqual>
<LessEqual> ::= <UnAmb> LEQ <UnAmb> | <UnAmb> LEQ <Lesser> | <UnAmb> LEQ <LessEqual>
<Greater> ::= <UnAmb> GE <UnAmb> | <UnAmb> GE <Greater> | <UnAmb> GE <GreatEqual>
<GreatEqual> ::= <UnAmb> GEQ <UnAmb> | <UnAmb> GEQ <Greater> | <UnAmb> GEQ <GreatEqual>
<Eq> ::= <UnAmb> EQ <UnAmb> | <UnAmb> EQ <Eq>
<NotEq> ::= <UnAmb> NEQ <UnAmb>
<Subtract> ::= <UnAmb> NEG <UnAmb> | <Add> NEG <UnAmb>
<Add> ::= <UnAmb> ADD <UnAmb> | <UnAmb> ADD <Add>
<Divide> ::= <UnAmb> DIV <UnAmb> | <Multiply> DIV <UnAmb>
<Multiply> ::= <UnAmb> MUL <UnAmb> | <UnAmb> MUL <Multiply>
<Power> ::= <UnAmb> POW <UnAmb>
<Modulo> ::= <UnAmb> MOD <UnAmb>
<Xor> ::= <UnAmb> XOR <UnAmb> | <UnAmb> XOR <Xor>
<Xand> ::= <UnAmb> XAND <UnAmb> | <UnAmb> XAND <Xand>
<LeftShift> ::= <Unamb> LSHIFT <Unamb>
<RightShift> ::= <Unamb> RSHIFT <Unamb>
<UnaryNeg> ::= NEG <UnAmb> | NEG <UnaryNeg>
<UnAmb> ::= IDEN | <Number> | <Bool> | <Arr> | LPAREN <Exp> RPAREN | <FunCall> | <ArrAcc>
<Loc> ::= IDEN | <ArrAcc>
<ArrAcc> ::= IDEN DeclMats
<Arr> ::= LBRACE Elems RBRACE
<Elems> ::= <Val> COMMA <Elems> | <Val> COMMA | <Val>
<Number> ::= DEC | BIN | HEX | OCT
<Bool> ::= TRUE | FALSE
<GenType> ::= <PrimType> | <ArrGenType>
<DeclType> ::= <PrimType> | <ArrDeclType>
<ArrGenType> ::= <PrimType> <Mats>
<Mats> ::= LBOX RBOX | LBOX RBOX <Mats>
<ArrDeclType> ::= <PrimType> <DeclMats>
<DeclMats> ::= <DeclMat> | <DeclMat> <DeclMats>
<DeclMat> ::= LBOX <Val> RBOX
<PrimType> ::= TBOOL | TI8 | TI16 | TI32 | TI64 | TI128 | TU8 | TU16 | TU32 | TU64 | TU128 | TF8 | TF16 | TF32 | TF64 | TF128 | TCHAR