<Prog> ::= <Decls>
<Block> ::= LBRACE <Decls> RBRACE
<Decls> ::= <Decl> <Decls> | <Decl>
<Decl> ::= <ConstDecl> | <VarDecl> | <FunDecl> | <Stmt>
<Stmt> ::= <ExpStmt> | <LogStmt> | <RetStmt> | <Cond> | <Loop>
<Loop> ::= WHILE <Val> <Block>
<Cond> ::= IF <Val> <Block> | IF <Val> <Block> <Else> | IF <Val> <Block> <Elifs> | IF <Val> <Block> <Elifs> <Else>
<Elifs> ::= <Elif> | <Elif> <Elifs>
<Elif> ::= ELIF <Val> <Block>
<Else> ::= ELSE <Block>
<RetStmt> ::= RETURN <Val> SEMC
<LogStmt> ::= LOG <Val> SEMC
<ExpStmt> ::= <Val> SEMC
<FunDecl> ::= DEFINE <Type> IDEN LPAREN <DeclParams> RPAREN <Block>
<DeclParams> ::= <DeclParam> | <DeclParam> COMMA <DeclParams>
<DeclParam> ::= <Type> IDEN
<VarDecl> ::= VAR <DeclType> IDEN ASSIGN <Val> SEMC | VAR <DeclType> IDEN SEMC
<ConstDecl> ::= CONST <DeclType> IDEN ASSIGN <Val> SEMC
<CallParams> ::= <Val> | <Val> COMMA <CallParams>
<Elems> ::= <Val> | <Val> COMMA <Elems>
<Val> ::= <Exp> | <Assn>
<Assn> ::= <Loc> ASSIGN <Val>
<Exp> ::= <UOp> | <BinOp> | <UnAmb>
<BinOp> ::= <Shift> | <Xand> | <Xor> | <And> | <Or> | <Mod> | <Pow> | <Sub> | <Div> | <Add> | <Mul> | <Eq> | <Neq> | <Less> | <Great>
<Great> ::= <UnAmb> GT <UnAmb> | <UnAmb> GTE <UnAmb> | <UnAmb> GT <Great> | <UnAmb> GTE <Great>
<Less> ::= <UnAmb> LT <UnAmb> | <UnAmb> LTE <UnAmb> | <UnAmb> LT <Less> | <UnAmb> LTE <Less>
<Neq> ::= <Eq> NEQ <UnAmb> | <UnAmb> NEQ <UnAmb>
<Eq> ::= <UnAmb> EQ <Eq> | <UnAmb> EQ <UnAmb>
<Div> ::= <Mul> DIV <UnAmb> | <UnAmb> DIV <UnAmb>
<Sub> ::= <Add> NEG <UnAmb> | <UnAmb> NEG <UnAmb>
<Mul> ::= <UnAmb> MUL <Mul> | <UnAmb> MUL <UnAmb>
<Add> ::= <UnAmb> ADD <Add> | <UnAmb> ADD <UnAmb>
<Pow> ::= <UnAmb> POW <UnAmb>
<Mod> ::= <UnAmb> MOD <UnAmb>
<Or> ::= <UnAmb> OR <Or> | <UnAmb> OR <UnAmb>
<And> ::= <UnAmb> AND <And> | <UnAmb> AND <UnAmb>
<Xor> ::= <UnAmb> XOR <Xor> | <UnAmb> XOR <UnAmb>
<Xand> ::= <UnAmb> XAND <UnAmb>
<Shift> ::= <LShift> | <RShift>
<RShift> ::= <UnAmb> RSHIFT <UnAmb>
<LShift> ::= <UnAmb> LSHIFT <UnAmb>
<UOp> ::= <UnSign> | <UnNot> | <PtrDeref> | <FunCall> | <ArrAcc> | <Ptr>
<FunCall> ::= <UnAmb> <Calls>
<Calls> ::= <Call> <Calls> | <Call>
<Call> ::= LPAREN <CallParams> RPAREN | LPAREN RPAREN
<Loc> ::= <ArrAcc> | IDEN | <PtrDeref>
<PtrDeref> ::= AT <UnAmb>
<UnNot> ::= NOT <UnNot> | NOT <UnAmb>
<UnSign> ::= ADD <UnSign> | NEG <UnSign> | ADD <UnAmb> | NEG <UnAmb>
<UnAmb> ::= <Atom> | <Arr> | <Ptr> | IDEN | <ArrAcc> | LPAREN <Exp> RPAREN
<SigParams> ::= <Type> | <Type> COMMA <SigParams>
<Type> ::= <AtomType> | <CompType>
<DeclType> ::= <AtomType> | <FunType> | <ArrDeclType> | <PtrType>
<CompType> ::= <FunType> | <ArrType> | <PtrType>
<ArrDeclType> ::= <AtomType> <VMats> | <FunType> <VMats> | <PtrType> <VMats>
<ArrType> ::= <AtomType> <Mats> | <FunType> <Mats> | <PtrType> <Mats>
<PtrType> ::= HASH <Type>
<VMats> ::= <VMat> <VMats> | <VMat>
<VMat> ::= LBOX <Val> RBOX
<Mats> ::= LBOX RBOX <Mats> | LBOX RBOX
<FunType> ::= TFN LPAREN <SigParams> RPAREN RETURNS <Type> | TFN LPAREN RPAREN RETURNS <Type>
<Comp> ::= <Arr> | <Ptr>
<Ptr> ::= HASH <Loc>
<ArrAcc> ::= <Arr> <VMats> | IDEN <VMats> | LPAREN <Exp> RPAREN <VMats>
<Arr> ::= LBRACE <Elems> RBRACE
<AtomType> ::= NULL | TBOOL | TCHAR | TU8 | TU16 | TU32 | TU64 | TU128 | TI8 | TI16 | TI32 | TI64 | TI128 | TF16 | TF32 | TF64 | TF128
<Atom> ::= NULL | <Bool> | <Char> | <Number>
<Bool> ::= TRUE | FALSE
<Char> ::= CHAR
<Number> ::= BIN | OCT | DEC | HEX